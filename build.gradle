// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath deps.kotlin.plugin
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.0'
        classpath 'com.google.firebase:perf-plugin:1.3.4'  // Performance Monitoring plugin
        classpath deps.androidx.navigation.plugin
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0"
        classpath "org.jacoco:org.jacoco.core:0.8.6"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "$rootDir/sonar.gradle"

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {

    def flavorsConfig = {
        apply from: "$rootDir/flavor.gradle"

        android {
            lintOptions {
                xmlReport true
            }
        }
    }
    pluginManager.withPlugin("com.android.application", flavorsConfig)
    pluginManager.withPlugin("com.android.library", flavorsConfig)

    apply from: "$rootDir/config/detekt/detekt.gradle"
    apply from: "$rootDir/config/ktlint/ktlint.gradle"

    apply from: "$rootDir/jacoco.gradle"

    sonarqube {
        properties  {
            property "sonar.androidLint.reportPaths", "$buildDir/reports/lint-results.xml"
            property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/**/*.xml"
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
